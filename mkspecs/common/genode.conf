#
# qmake configuration for common genode
#

MAKEFILE_GENERATOR      = UNIX
QMAKE_PLATFORM          = genode freebsd unix posix
CONFIG                 += unversioned_libname unversioned_soname

# static libraries currently only work as 'lib*.a'
QMAKE_PREFIX_PLUGIN       = lib
QMAKE_EXTENSION_PLUGIN    = lib.so
QMAKE_PREFIX_SHLIB        = lib
QMAKE_EXTENSION_SHLIB     = lib.so
QMAKE_PREFIX_STATICLIB    = lib
QMAKE_EXTENSION_STATICLIB = a

include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

# modifications to g++.conf
QMAKE_CC                = $$(GENODE_QMAKE_CC)
QMAKE_CXX               = $$(GENODE_QMAKE_CXX)
QMAKE_LINK              = $$(GENODE_QMAKE_LINK)

QMAKE_AR                = $$(GENODE_QMAKE_AR) cqs
QMAKE_OBJCOPY           = $$(GENODE_QMAKE_OBJCOPY)
QMAKE_NM                = $$(GENODE_QMAKE_NM) -P
QMAKE_STRIP             = $$(GENODE_QMAKE_STRIP)

QMAKE_LFLAGS_RPATH      =

QMAKE_CFLAGS            += $$(GENODE_QMAKE_CFLAGS)
QMAKE_CXXFLAGS          += $$(GENODE_QMAKE_CXXFLAGS)
QMAKE_LFLAGS_APP        += $$(GENODE_QMAKE_LFLAGS_APP)
QMAKE_LFLAGS_SHLIB      += $$(GENODE_QMAKE_LFLAGS_SHLIB)
QMAKE_LIBS              += $$(GENODE_QMAKE_LIBS)
QMAKE_INCDIR_OPENGL     += $$(GENODE_QMAKE_INCDIR_OPENGL)
QMAKE_LIBS_OPENGL       += $$(GENODE_QMAKE_LIBS_OPENGL)
QMAKE_INCDIR_OPENGL_ES2 += $$(GENODE_QMAKE_INCDIR_OPENGL)
QMAKE_LIBS_OPENGL_ES2   += $$(GENODE_QMAKE_LIBS_OPENGL)
QMAKE_INCDIR_EGL        += $$(GENODE_QMAKE_INCDIR_EGL)
QMAKE_LIBS_EGL          += $$(GENODE_QMAKE_LIBS_EGL)

QMAKE_CXXFLAGS          += $$QMAKE_CFLAGS
QMAKE_CXXFLAGS          += -fpermissive

QMAKE_LFLAGS_PLUGIN     = $$QMAKE_LFLAGS_SHLIB

load(qt_config)
